#!/usr/bin/env perl
# Copyright (C) 2014-2016 all contributors <meta@public-inbox.org>
# License: AGPL-3.0+ <https://www.gnu.org/licenses/agpl-3.0.txt>
#
# Stupid script to make HTML from preformatted, utf-8 text versions,
# only generating links for http(s).  Markdown does too much
# and requires indentation to output preformatted text.
use strict;
use warnings;
use Encode qw/encode/;
my $str = eval { local $/; <> };
my %xhtml_map = (
	'"' => '&#34;',
	'&' => '&#38;',
	"'" => '&#39;',
	'<' => '&lt;',
	'>' => '&gt;',
);
$str =~ s/([<>&'"])/$xhtml_map{$1}/ge;
$str = encode('us-ascii', $str, Encode::HTMLCREF);
my ($title) = ($str =~ /\A([^\n]+)/);

# temporarily swap &gt; for escape so our s!! to add href works.
# there's probably a way to do this with only a single s!! ...
$str =~ s!&gt;!\e!g;
$str =~ s!\b((nntp|ftp|https?)://[\w+\+\&\?\.\%\;/#-]+)!<a
href="$1"\n>$1</a>!g;

$str =~ s!\e!&gt;!g; # swap escapes back to &gt;

print '<html><head>',
  '<meta http-equiv="Content-Type" content="text/html; charset=utf-8" />',
  "<title>$title</title>",
  "</head><body>\n<pre>",  $str , '</pre></body></html>';
