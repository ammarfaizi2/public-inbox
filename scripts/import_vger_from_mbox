#!/usr/bin/perl -w
# Copyright (C) 2016-2018 all contributors <meta@public-inbox.org>
# License: AGPL-3.0+ <https://www.gnu.org/licenses/agpl-3.0.txt>
use strict;
use warnings;
use Getopt::Long qw/:config gnu_getopt no_ignore_case auto_abbrev/;
use Date::Parse qw/str2time/;
use Email::MIME;
$Email::MIME::ContentType::STRICT_PARAMS = 0; # user input is imperfect
use PublicInbox::V2Writable;
my $usage = "usage: $0 NAME EMAIL DIR <MBOX\n";
my $dry_run;
my %opts = ( 'n|dry-run' => \$dry_run );
GetOptions(%opts) or die $usage;
my $name = shift or die $usage; # git
my $email = shift or die $usage; # git@vger.kernel.org
my $mainrepo = shift or die $usage; # /path/to/v2/repo
my $v2ibx = {
	mainrepo => $mainrepo,
	name => $name,
	-primary_address => $email,
};
my $im = $dry_run ? undef : PublicInbox::V2Writable->new($v2ibx, 1);
binmode STDIN;
my $msg = '';
use PublicInbox::Filter::Vger;
my $vger = PublicInbox::Filter::Vger->new;

sub do_add ($$) {
	my ($im, $msg) = @_;
	$$msg =~ s/(\r?\n)+\z/$1/s;
	$msg = Email::MIME->new($$msg);
	$msg = $vger->scrub($msg);
	return unless $im;
	$im->add($msg) or
		warn "duplicate: ",
			$msg->header_obj->header_raw('Message-ID'), "\n";
}

# asctime: From example@example.com Fri Jun 23 02:56:55 2000
my $from_strict = qr/^From \S+ +\S+ \S+ +\S+ [^:]+:[^:]+:[^:]+ [^:]+/;
my $prev = undef;
while (defined(my $l = <STDIN>)) {
	if ($l =~ /$from_strict/o) {
		if (!defined($prev) || $prev =~ /^\r?$/) {
			do_add($im, \$msg) if $msg;
			$msg = '';
			$prev = $l;
			next;
		}
		warn "W[$.] $l\n";
	}
	$prev = $l;
	$msg .= $l;
}
do_add($im, \$msg) if $msg;
$im->done if $im;
