#!/usr/bin/perl -w
# Copyright (C) 2013, Eric Wong <normalperson@yhbt.net> and all contributors
# License: AGPLv3 or later (https://www.gnu.org/licenses/agpl-3.0.txt)
#
# One-off script to convert an slrnpull news spool from gmane, usage:
=begin usage
	mkdir -p $HOME/.public-inbox
	MAINREPO=/path/to/your/repo.git
	export RECIPIENT='list@example.com'
	git init --bare $MAINREPO
	export GIT_CONFIG=$HOME/.public-inbox/config
	git config publicinbox.$LISTNAME.address $RECIPIENT
	git config publicinbox.$LISTNAME.mainrepo $MAINREPO
	unset GIT_CONFIG
	./import_gmane_spool SLRNPULL_ROOT/news/foo/bar
=cut
use strict;
use warnings;
use Parallel::ForkManager;
use Email::Simple;
use PublicInbox::Filter;
use PublicInbox::Config;
use IPC::Run qw(run);
sub usage { "Usage:\n".join("",grep(/\t/, `head -n 24 $0`)) }
my $spool = shift @ARGV or die usage();
my $nproc = `nproc 2>/dev/null` || 4;
my $pm = Parallel::ForkManager->new($nproc);
defined $ENV{RECIPIENT} or die usage();
my @args = ('public-inbox-mda');

foreach my $n (glob("$spool/*")) {
	$n =~ m{/\d+\z} or next;
	$pm->start and next;
	if (open my $fh, '<', $n) {
		my $s = eval {
			local $/;
			Email::Simple->new(<$fh>);
		};

		# gmane rewrites Received headers, which increases spamminess
		my @h = $s->header("Original-Received");
		if (@h) {
			$s->header_set("Received", @h);
			$s->header_set("Original-Received");
		}

		# triggers for the SA HEADER_SPAM rule
		foreach my $drop (qw(Approved)) { $s->header_set($drop) }

		# appears to be an old gmane bug:
		$s->header_set("connect()");

		my $orig = $s->as_string;
		close $fh or die "close failed: $!\n";
		eval { run(\@args, \$orig) };
		die "fail $n: $?\n" if $?;
		die "fail $n: $@\n" if $@;
	} else {
		warn "Failed to open $n: $!\n";
	}
	$pm->finish;
}

$pm->wait_all_children;
