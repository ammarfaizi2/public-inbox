#!/usr/bin/perl -w
# Copyright (C) 2013-2019 all contributors <meta@public-inbox.org>
# License: AGPL-3.0+ <https://www.gnu.org/licenses/agpl-3.0.txt>
#
# Mail delivery agent for public-inbox, run from your MTA upon mail delivery
use strict;
use warnings;
my $usage = 'public-inbox-mda [OPTIONS] < rfc2822_message';
my $precheck = grep(/\A--no-precheck\z/, @ARGV) ? 0 : 1;
my ($ems, $emm);

sub do_exit {
	my ($code) = shift;
	$emm = $ems = undef; # trigger DESTROY
	exit $code;
}

use Email::Simple;
use PublicInbox::MIME;
use PublicInbox::MDA;
use PublicInbox::Config;
use PublicInbox::Emergency;
use PublicInbox::Filter::Base;
use PublicInbox::InboxWritable;
use PublicInbox::Spamcheck;

# n.b: hopefully we can setup the emergency path without bailing due to
# user error, we really want to setup the emergency destination ASAP
# in case there's bugs in our code or user error.
my $emergency = $ENV{PI_EMERGENCY} || "$ENV{HOME}/.public-inbox/emergency/";
$ems = PublicInbox::Emergency->new($emergency);
my $str = eval { local $/; <STDIN> };
$str =~ s/\A[\r\n]*From [^\r\n]*\r?\n//s;
$ems->prepare(\$str);
my $simple = Email::Simple->new(\$str);
my $config = PublicInbox::Config->new;
my $key = 'publicinboxmda.spamcheck';
my $default = 'PublicInbox::Spamcheck::Spamc';
my $spamc = PublicInbox::Spamcheck::get($config, $key, $default);
my $dst;
my $recipient = $ENV{ORIGINAL_RECIPIENT};
if (defined $recipient) {
	$dst = $config->lookup($recipient); # first check
}
if (!defined $dst) {
	$dst = PublicInbox::MDA->inbox_for_list_id($config, $simple);
	if (!defined $dst && !defined $recipient) {
		die "ORIGINAL_RECIPIENT not defined in ENV\n";
	}
	defined $dst or do_exit(67); # EX_NOUSER 5.1.1 user unknown
}

$dst = PublicInbox::InboxWritable->new($dst);
eval { $dst->assert_usable_dir };
do_exit(67) if $@;

# pre-check, MDA has stricter rules than an importer might;
if ($precheck && !PublicInbox::MDA->precheck($simple, $dst->{address})) {
	do_exit(0);
}
$simple = undef;
my $spam_ok;
if ($spamc) {
	$str = '';
	$spam_ok = $spamc->spamcheck($ems->fh, \$str);
	# update the emergency dump with the new message:
	$emm = PublicInbox::Emergency->new($emergency);
	$emm->prepare(\$str);
	$ems = $ems->abort;
} else { # no spam checking configured:
	$spam_ok = 1;
	$emm = $ems;
	my $fh = $emm->fh;
	read($fh, $str, -s $fh);
}
do_exit(0) unless $spam_ok;

my $mime = PublicInbox::MIME->new(\$str);

# -mda defaults to the strict base filter which we won't use anywhere else
sub mda_filter_adjust ($) {
	my ($ibx) = @_;
	my $fcfg = $ibx->{filter} || '';
	if ($fcfg eq '') {
		$ibx->{filter} = 'PublicInbox::Filter::Base';
	} elsif ($fcfg eq 'scrub') { # legacy alias, undocumented, remove?
		$ibx->{filter} = 'PublicInbox::Filter::Mirror';
	}
}

mda_filter_adjust($dst);

my $filter = $dst->filter;
my $ret = $filter->delivery($mime);
if (ref($ret) && $ret->isa('Email::MIME')) { # filter altered message
	$mime = $ret;
} elsif ($ret == PublicInbox::Filter::Base::IGNORE) {
	do_exit(0); # chuck it to emergency
} elsif ($ret == PublicInbox::Filter::Base::REJECT) {
	$! = 65; # EX_DATAERR 5.6.0 data format error
	die $filter->err, "\n";
} # else { accept
$filter = undef;

PublicInbox::MDA->set_list_headers($mime, $dst);
my $im = $dst->importer(0);
if (defined $im->add($mime)) {
	$emm = $emm->abort;
} else {
	# this message is similar to what ssoma-mda shows:
	print STDERR "CONFLICT: Message-ID: ",
			$mime->header_obj->header_raw('Message-ID'),
			" exists\n";
}

$im->done;
do_exit(0);
