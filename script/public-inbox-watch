#!/usr/bin/perl -w
# Copyright (C) all contributors <meta@public-inbox.org>
# License: AGPL-3.0+ <https://www.gnu.org/licenses/agpl-3.0.txt>
my $help = <<EOF;
usage: public-inbox-watch

See public-inbox-watch(1) man page for full documentation.
EOF

use strict;
use Getopt::Long qw(:config gnu_getopt no_ignore_case auto_abbrev);
use IO::Handle; # ->autoflush
use PublicInbox::Watch;
use PublicInbox::Config;
use PublicInbox::DS;
my $do_scan = 1;
GetOptions('scan!' => \$do_scan, # undocumented, testing only
	'help|h' => \(my $show_help)) or do { print STDERR $help; exit 1 };
if ($show_help) { print $help; exit 0 };
PublicInbox::DS::block_signals();
STDOUT->autoflush(1);
STDERR->autoflush(1);
local $0 = $0; # local since this script may be eval-ed
my $watch = PublicInbox::Watch->new(PublicInbox::Config->new);
my $reload = sub {
	my $prev = $watch or return; # SIGQUIT issued
	$watch->quit;
	$watch = PublicInbox::Watch->new(PublicInbox::Config->new);
	if ($watch) {
		$watch->{sig} = $prev->{sig}; # prevent redundant signalfd
		warn "# reloaded\n";
	} else {
		warn("E: reloading failed\n");
		$watch = $prev;
	}
};

if ($watch) {
	my $scan = sub {
		return if !$watch;
		warn "# scanning\n";
		$watch->trigger_scan('full');
	};
	my $quit = sub {
		$watch->quit if $watch;
		$watch = undef;
		$0 .= ' quitting';
	};
	my $sig = {
		HUP => $reload,
		USR1 => $scan,
		CHLD => \&PublicInbox::DS::enqueue_reap,
	};
	$sig->{QUIT} = $sig->{TERM} = $sig->{INT} = $quit;
	local @SIG{keys %$sig} = values(%$sig); # for non-signalfd/kqueue

	# --no-scan is only intended for testing atm, undocumented.
	PublicInbox::DS::requeue($scan) if $do_scan;
	$watch->watch($sig) while ($watch);
}
