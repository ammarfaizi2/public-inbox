#!/usr/bin/perl -w
# Copyright (C) 2014-2015 all contributors <meta@public-inbox.org>
# License: AGPLv3 or later (https://www.gnu.org/licenses/agpl-3.0.txt)
#
# Used for training spam (via SpamAssassin) and removing messages from a
# public-inbox
my $usage = "$0 (spam|ham) < /path/to/message";
use strict;
use warnings;
use PublicInbox::Config;
use PublicInbox::Git;
use PublicInbox::Import;
use Email::MIME;
use Email::MIME::ContentType;
$Email::MIME::ContentType::STRICT_PARAMS = 0; # user input is imperfect
use PublicInbox::Address;
use PublicInbox::Spamcheck::Spamc;
my $train = shift or die "usage: $usage\n";
if ($train !~ /\A(?:ham|spam)\z/) {
	die "`$train' not recognized.\nusage: $usage\n";
}

my $spamc = PublicInbox::Spamcheck::Spamc->new;
my $pi_config = PublicInbox::Config->new;
my $err;
my $mime = Email::MIME->new(eval {
	local $/;
	my $data = scalar <STDIN>;
	$data =~ s/\AFrom [^\r\n]*\r?\n//s;
	eval {
		if ($train eq 'ham') {
			$spamc->hamlearn(\$data);
		} else {
			$spamc->spamlearn(\$data);
		}
		die "spamc failed with: $?\n" if $?;
	};
	$err = $@;
	$data
});

# get all recipients
my %dests;
foreach my $h (qw(Cc To)) {
	my $val = $mime->header($h) or next;
	foreach my $email (PublicInbox::Address::emails($val)) {
		$dests{lc($email)} = 1;
	}
}

require PublicInbox::MDA if $train eq "ham";

# n.b. message may be cross-posted to multiple public-inboxes
foreach my $recipient (keys %dests) {
	my $dst = $pi_config->lookup($recipient) or next;
	my $git_dir = $dst->{mainrepo} or next;
	my $git = PublicInbox::Git->new($git_dir);
	# We do not touch GIT_COMMITTER_* env here so we can track
	# who trained the message.
	my $name = $ENV{GIT_COMMITTER_NAME} || $dst->{inbox};
	my $email = $ENV{GIT_COMMITTER_EMAIL} || $recipient;
	my $im = PublicInbox::Import->new($git, $name, $email);

	if ($train eq "spam") {
		# This needs to be idempotent, as my inotify trainer
		# may train for each cross-posted message, and this
		# script already learns for every list in
		# ~/.public-inbox/config
		$im->remove($mime);
	} else { # $train eq "ham"
		# no checking for spam here, we assume the message has
		# been reviewed by a human at this point:
		PublicInbox::MDA->set_list_headers($mime, $dst);

		# Ham messages are trained when they're marked into
		# a SEEN state, so this is idempotent:
		$im->add($mime);
	}
	$im->done;
}

if ($err) {
	warn $err;
	exit 1;
}
