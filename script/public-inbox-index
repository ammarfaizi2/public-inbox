#!/usr/bin/perl -w
# Copyright (C) 2015-2019 all contributors <meta@public-inbox.org>
# License: AGPL-3.0+ <https://www.gnu.org/licenses/agpl-3.0.txt>
# Basic tool to create a Xapian search index for a git repository
# configured for public-inbox.
# Usage with libeatmydata <https://www.flamingspork.com/projects/libeatmydata/>
# highly recommended: eatmydata public-inbox-index INBOX_DIR

use strict;
use warnings;
use Getopt::Long qw(:config gnu_getopt no_ignore_case auto_abbrev);
my $usage = "public-inbox-index INBOX_DIR";
use PublicInbox::Admin;
PublicInbox::Admin::require_or_die('-index');

my $opt = {};
GetOptions($opt, qw(reindex jobs|j=i prune indexlevel|L=s))
	or die "bad command-line args\n$usage";
die "--jobs must be positive\n" if defined $opt->{jobs} && $opt->{jobs} <= 0;

sub usage { print STDERR "Usage: $usage\n"; exit 1 }

# do we really care about this message?  It's annoying...
my $warn = 'public-inbox unconfigured for serving, indexing anyways...';
my @ibxs = PublicInbox::Admin::resolve_inboxes(\@ARGV, $warn);
PublicInbox::Admin::require_or_die('-index');
usage() unless @ibxs;
my $mods = {};
foreach my $ibx (@ibxs) {
	if (defined $opt->{indexlevel} && !defined($ibx->{indexlevel})) {
		# XXX: users can shoot themselves in the foot, with this...
		$ibx->{indexlevel} = $opt->{indexlevel};
	}
	PublicInbox::Admin::scan_ibx_modules($mods, $ibx);
}

PublicInbox::Admin::require_or_die(keys %$mods);
PublicInbox::Admin::index_inbox($_, $opt) for @ibxs;
