#!/usr/bin/perl -w
# Copyright (C) 2015 all contributors <meta@public-inbox.org>
# License: AGPLv3 or later (https://www.gnu.org/licenses/agpl-3.0.txt)
# Basic tool to create a Xapian search index for a git repository
# configured for public-inbox.
# Usage with libeatmydata <https://www.flamingspork.com/projects/libeatmydata/>
# highly recommended: eatmydata public-inbox-index GIT_DIR

use strict;
use warnings;
my $usage = "public-inbox-index GIT_DIR";
use PublicInbox::Config;
eval { require PublicInbox::SearchIdx };
if ($@) {
	print STDERR "Search::Xapian required for $0\n";
	exit 1;
}
my @dirs;

sub resolve_git_dir {
	my ($cd) = @_;
	my @cmd = qw(git rev-parse --git-dir);
	my $cmd = join(' ', @cmd);
	my $pid = open my $fh, '-|';
	defined $pid or die "forking $cmd failed: $!\n";
	if ($pid == 0) {
		if (defined $cd) {
			chdir $cd or die "chdir $cd failed: $!\n";
		}
		exec @cmd;
		die "Failed to exec $cmd: $!\n";
	} else {
		my $dir = eval {
			local $/;
			<$fh>;
		};
		close $fh or die "error in $cmd: $!\n";
		chomp $dir;
		return $cd if ($dir eq '.' && defined $cd);
		$dir;
	}
}

if (@ARGV) {
	@dirs = map { resolve_git_dir($_) } @ARGV;
} else {
	@dirs = (resolve_git_dir());
}

sub usage { print STDERR "Usage: $usage\n"; exit 1 }
usage() unless @dirs;

foreach my $dir (@dirs) {
	index_dir($dir);
}

sub index_dir {
	my ($git_dir) = @_;
	-d $git_dir or die "$git_dir does not appear to be a git repository\n";
	my $s = PublicInbox::SearchIdx->new($git_dir, 1);
	$s->index_sync;
}
